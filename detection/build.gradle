plugins {
    id 'com.android.library'
    id 'maven-publish'
    id "digital.wup.android-maven-publish" version "3.6.2"
}

apply from: '..//core/dependencies.gradle'

android {
    compileSdk project.compileSdk

    defaultConfig {
        minSdk project.minSdk
        targetSdk project.targetSdk
        versionCode buildVersionCode(release_version)
        versionName release_version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    compileOnly project(path: ':core')

    testImplementation "junit:junit:${junit}"
    androidTestImplementation "androidx.test.ext:junit:${extJunit}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${espressoCore}"
}

project.afterEvaluate {
    publishing {
        publications {
            library(MavenPublication) {
                setGroupId 'com.gravity.face'
                //You can either define these here or get them from project conf elsewhere
                setArtifactId 'detection'
                version android.defaultConfig.versionName
                artifact bundleReleaseAar //aar artifact you want to publish

                pom.withXml {
                    asNode().appendNode('name', 'Core')
                    asNode().appendNode('description', 'Core Module Solution For Android From Mediapipe')
                    asNode().appendNode('url', 'https://github.com/shayantabatabaee/Face-ML')
                    def licence = asNode().appendNode('licenses').appendNode('license')
                    licence.appendNode('name', 'The Apache License, Version 2.0')
                    licence.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')

                    def developer = asNode().appendNode('developers').appendNode('developer')
                    developer.appendNode('name', 'Shayan Tabatabaee')
                    developer.appendNode('email', 'shayan.ta69@gmail.com')

                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}