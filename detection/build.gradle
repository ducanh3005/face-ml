plugins {
    id 'com.android.library'
    id 'maven-publish'
    id "digital.wup.android-maven-publish" version "3.6.2"
}

apply from: '..//core/dependencies.gradle'

android {
    compileSdk project.compileSdk

    defaultConfig {
        minSdk project.minSdk
        targetSdk project.targetSdk
        versionCode buildVersionCode(release_version)
        versionName release_version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation project(path: ':core')

    compileOnly "org.tensorflow:tensorflow-lite:${tensorflowLite}"
    compileOnly "org.tensorflow:tensorflow-lite-support:${tensorflowLiteSupport}"

    testImplementation "junit:junit:${junit}"
    androidTestImplementation "androidx.test.ext:junit:${extJunit}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${espressoCore}"
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = 'Face Detection'
                description = 'Face Detection Solution For Android From Mediapipe'
                url = 'https://github.com/shayantabatabaee/Face-ML'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Shayan Tabatabaee'
                        email = 'shayan.ta69@gmail.com'
                    }
                }
            }
            groupId 'com.gravity.face'
            version release_version
            artifactId = "$project.name"
            artifact("$buildDir/outputs/aar/$project.name-release.aar")
            from components.android
        }
    }
}